DIF:100*(EMA(CLOSE,SHORT)-EMA(CLOSE,LONG));
DEA:EMA(DIF,MID);
MACD:(DIF-DEA)*2,COLORSTICK;

ZC:=CONST(BARSCOUNT(CLOSE)); { 总共K线的数量 }

金叉:=CROSS(DIF,DEA);
死叉:=CROSS(DEA,DIF);

{DRAWTEXT(金叉, HIGH * 2,'金叉');
DRAWTEXT(死叉, HIGH * 1,'死叉');}

B1:=BARSLAST(死叉); {最近一次死叉的位置}
B2:=REF(BARSLAST(死叉), B1 + 1) + B1 + 1; {倒数第二次死叉与倒数第一次死叉的区间}
B3:=REF(BARSLAST(死叉), B2 + 1) + B2 + 1; {倒数第三次死叉与倒数第二次死叉的区间}

T1:=BARSLAST(金叉); {最近一次金叉的位置}
T2:=REF(BARSLAST(金叉), T1 + 1) + T1 + 1; {倒数第二次金叉与倒数第一次金叉的区间}
T3:=REF(BARSLAST(金叉), T2 + 1) + T2 + 1; {倒数第三次金叉与倒数第二次金叉的区间}

BCL1:=LLV(CLOSE, B1 + 1); {最近一次死叉区间的最低收盘价}
BCL2:=REF(BCL1, T1 + 1); {倒数第二次死叉区间的最低收盘价}
BCL3:=REF(BCL1, T2 + 1); {倒数第三次死叉区间的最低收盘价}

BIL1:=LLV(DIF, B1 + 1); {最近一次死叉区间的最低DIF}
BIL2:=REF(BIL1, B1 + 1); {倒数第二次死叉区间的最低DIF}
BIL3:=REF(BIL1, B2 + 1); {倒数第三次死叉区间的最低DIF}

BEL1:=LLV(DEA, B1 + 1); {最近一次死叉区间的最低DEA}
BEL2:=REF(BEL1, B1 + 1); {倒数第二次死叉区间的最低DEA}
BEL3:=REF(BEL1, B2 + 1); {倒数第三次死叉区间的最低DEA}

TCH1:=HHV(CLOSE, T1 + 1); {最近一次金叉区间的最高收盘价}
TCH2:=REF(TCH1, B1 + 1); {倒数第二次金叉区间的最高收盘价}
TCH3:=REF(TCH1, B2 + 1); {倒数第三次金叉区间的最高收盘价}

TIH1:=HHV(DIF, T1 + 1); {最近一次金叉区间的最高DIF}
TIH2:=REF(TIH1, T1 + 1); {倒数第二次金叉区间的最高DIF}
TIH3:=REF(TIH1, T2 + 1); {倒数第三次金叉区间的最高DIF}

TEH1:=HHV(DIF, T1 + 1); {最近一次金叉区间的最高DEA}
TEH2:=REF(TEH1, T1 + 1); {倒数第二次金叉区间的最高DEA}
TEH3:=REF(TEH1, T2 + 1); {倒数第三次金叉区间的最高DEA}

剩余区域长度:=IF(ISVALID(BARSNEXT(金叉 OR 死叉)), BARSNEXT(金叉 OR 死叉) - 1, ZC - BARSCOUNT(C));

{1.2 底背离逻辑处理} 

{1.2.1 最近的底部区域与倒数第二个底部区域比较} 
区域最低DIF:=IF(剩余区域长度 < 0, BIL1, REFX(LLV(DIF, B1), 剩余区域长度));
区域最低DEA:=IF(剩余区域长度 < 0, BEL1, REFX(LLV(DEA, B1), 剩余区域长度));

钝化区域BIL:=LLV(DIF, B1); {最近一次金叉区间的钝化区间的最低DIF, 钝化区间是第二根K线开始的区间}
钝化最低DIF:=IF(剩余区域长度 < 0, 钝化区域BIL, REFX(LLV(DIF, B1 - 1), 剩余区域长度));

{1.2.1.1 找到底钝化点(寻找比前一个底部区域最低收盘价更低的收盘价, 
                      从这个收盘价开始寻找一个DIF比前区域最低DIF更高的DIF)}
底钝化信号I1:=B1 >= 1 AND DIF < DEA AND BCL1 < BCL2 AND DIF > BIL2 AND BIL1 >= BIL2;
底钝化信号E1:=B1 >= 1 AND DIF < DEA AND BCL1 < BCL2 AND DEA > BEL2 AND BEL1 >= BEL2;

{1.2.1.2 找到底钝化消失点(找到比上一个底部区域更低的DIF)}
底钝消信号1:=B1 >= 2 AND DIF < DEA AND DIF < BIL2  AND REF(COUNT(底钝化信号I1, B1), 1) >= 1;

{1.2.1.3 找到底结构形成-70% (从钝化往后开始，只要找到一个DIF比前一根DIF更大的情况)}
真底70信号1:=B1 >= 2 AND REF(DIF, 1) < REF(DEA, 1) AND REF(DIF, 1) > BIL2 AND REF(DIF, 1) = 钝化最低DIF;
前真底70存在1:=ISVALID(BARSLAST(真底70信号1)) AND BARSLAST(真底70信号1) < B1;
前真底70不存在1:=前真底70存在1==0;
后真底70存在1:=ISVALID(REFX(BARSNEXT(真底70信号1), 1));
后真底70不存在1:=后真底70存在1=0;

虚底70信号1:=B1 >= 2 AND DIF < DEA AND DIF > REF(DIF, 1) AND DIF > BIL2 AND REF(DIF, 1) > 钝化最低DIF;
后虚底70存在1:=ISVALID(REFX(BARSNEXT(虚底70信号1), 1)) AND (后真底70不存在1 OR REFX(BARSNEXT(真底70信号1), 1) > REFX(BARSNEXT(虚底70信号1), 1)); 
后虚底70不存在1:=后虚底70存在1=0;

{1.2.1.4 判断顿消情况是否存在}
前底钝消次数1:=REF(COUNT(底钝消信号1, B1), 1);
总底钝消次数1:=REFX(COUNT(底钝消信号1, B1), 剩余区域长度);
底钝消不存在1:=前底钝消次数1=0 AND 总底钝消次数1=0;

{1.2.1.5 找到第一个有效的钝化点(钝化点之后如何存在结构反转的情况, 则钝化无效)
	     结构反转: 在出现底结构-70%之后，出现更小的DIF, 则视为结构反转}
底钝化有效1:=底钝化信号I1 AND 底钝消不存在1 AND (后真底70不存在1 OR (后真底70存在1 AND 后虚底70不存在1) OR BARSNEXT(虚底70信号1) < BARSNEXT(真底70信号1));
底钝化次数1:=COUNT(底钝化有效1, B1);

{1.2.1.6 判断是否需要比较倒数第三个区域}
总底钝化次数:=REFX(COUNT(底钝化有效1, B1), 剩余区域长度);
总收盘价小于:=REFX(BCL1, 剩余区域长度) < BCL2;
需底钝化检测:=总底钝化次数 = 0 AND 总收盘价小于;

{1.2.1.7 找到底结构形成-100% (如果未出现钝化消失，则在CROSS(DIF,DEA)时，标注结构形成-100%)};
底100形成1:=DIF > DEA AND REF(DIF, 1) < REF(DEA, 1) AND REF(底钝化次数1, 1) >= 1 AND 前真底70存在1;

{1.2.1.8 结构70形成后存在24个K线，那么在24周期内，标注24周期的数字，并且判断24周期内有没有新低，如果创了新低，则在新低位置标注底结构消失}
底结构形成1:=(真底70信号1 AND REF(底钝化次数1, 1) >= 1) OR (底100形成1 AND 前真底70不存在1);
底有效范围1:=BARSLAST(底结构形成1)>0 AND BARSLAST(底结构形成1)<=24;
底范围标记1:=BARSLAST(底结构形成1);
底收盘价格1:=REF(BCL1, BARSLAST(底结构形成1));

底结构消失信号1:=底有效范围1 AND CLOSE < 底收盘价格1;
底结构消失次数1:=COUNT(底结构消失信号1, 底范围标记1);
底结消存在1:=IF(ISVALID(BARSNEXT(CLOSE < 底收盘价格1)), BARSNEXT(CLOSE < 底收盘价格1) <= 24 - 底范围标记1, 0);
底结消不存在1:=底结消存在1==0 AND 底结构消失次数1==0;
底结构消失形成1:=底结消存在1 AND 底结构消失次数1==1 AND REF(底结构消失次数1, 1)==0;

{1.2.1.9 结构70形成后存在24个K线，那么在24周期内，底结构形成当前数字是0, 周期最后一个数字标注是24, 在这个周期范围内，第一次DIF >= 0，标注底平衡 }
底平衡次数1:=COUNT(DIF >= 0, 底范围标记1);
底平衡形成1:=底平衡次数1==1 AND DIF >= 0;

{1.2.1.10 标记所有与倒数第二个底部区域比较的标记点}
DRAWTEXT(底钝化有效1 AND 底钝化次数1=1, DIF * 0.7, IF(底钝化信号E1, '重底钝化', '底钝化')), COLORGREEN;
DRAWTEXT(真底70信号1 AND REF(底钝化次数1, 1) >= 1 AND DIF < DEA , DIF * 1.1, '底结构形成-70%'), COLORYELLOW;
DRAWTEXT(底100形成1, DIF * 0.3, '底结构形成-100%'), COLORMAGENTA;

DRAWTEXT(底结构消失形成1, DIF * 0.4, '底结构消失');
DRAWNUMBER(底结消不存在1 AND 底有效范围1, HIGH * 0.5, 底范围标记1);
DRAWTEXT(底结消不存在1 AND 底有效范围1 AND 底平衡形成1, 0, '底平衡'), COLORRED;

{1.2.2 最近的底部区域与倒数第三个底部区域比较} 

{1.2.2.1 找到底钝化点(寻找比倒数第三个底部区域最低收盘价更低的收盘价, 
                      从这个收盘价开始寻找一个DIF比倒数第三个区域最低DIF更高的DIF)}
底钝化信号I2:=B1 >= 1 AND DIF < DEA AND BCL1 < BCL3 AND DIF > BIL3 AND BIL1 >= BIL3;
底钝化信号E2:=B1 >= 1 AND DIF < DEA AND BCL1 < BCL3 AND DEA > BEL3 AND BEL1 >= BEL3;

{1.2.2.2 找到钝化消失点(找到比倒数第三个部区域更低的DIF)}
底钝消信号2:=B1 >= 2 AND DIF < DEA AND DIF < BIL3 AND REF(COUNT(底钝化信号I2, B1), 1) >= 1;

{1.2.2.3 标注底结构形成-70% (从钝化往后开始，只要找到一个DIF比前一根DIF更大的情况)}
真底70信号2:=B1 >= 2 AND REF(DIF, 1) < REF(DEA, 1) AND REF(DIF, 1) > BIL3 AND REF(DIF, 1) = 钝化最低DIF;
前真底70存在2:=ISVALID(BARSLAST(真底70信号2)) AND BARSLAST(真底70信号2) < B1;
前真底70不存在2:=前真底70存在2==0;
后真底70存在2:=ISVALID(REFX(BARSNEXT(真底70信号2), 1));
后真底70不存在2:=后真底70存在2=0;

虚底70信号2:=B1 >= 2 AND DIF > REF(DIF, 1) AND DIF < DEA AND DIF > BIL3 AND REF(DIF, 1) > 钝化最低DIF;
后虚底70存在2:=ISVALID(REFX(BARSNEXT(虚底70信号2), 1)) AND (后真底70不存在2 OR REFX(BARSNEXT(真底70信号2), 1) > REFX(BARSNEXT(虚底70信号2), 1));
后虚底70不存在2:=后虚底70存在2=0;

{1.2.2.4 判断顿消情况是否存在}
前底钝消次数2:=REF(COUNT(底钝消信号2, B1), 1);
总底钝消次数2:=REFX(COUNT(底钝消信号2, B1), 剩余区域长度);
底钝消不存在2:=前底钝消次数2=0 AND 总底钝消次数2=0;

{1.2.2.5 找到第一个有效的钝化点(钝化点之后如何存在结构反转的情况, 则钝化无效)
	     结构反转: 在出现底结构-70%之后，出现更小的DIF, 则视为结构反转}
底钝化有效2:=需底钝化检测 AND 底钝化信号I2 AND 底钝消不存在2 AND (后真底70不存在2 OR (后真底70存在2 AND 后虚底70不存在2) OR BARSNEXT(虚底70信号2) < BARSNEXT(真底70信号2));
底钝化次数2:=COUNT(底钝化有效2, B1);

{1.2.2.6 找到底结构形成-100% (如果未出现钝化消失，则在CROSS(DIF,DEA)时，标注结构形成-100%)};
底100形成2:=DIF > DEA AND REF(DIF, 1) < REF(DEA, 1) AND REF(底钝化次数2, 1) >= 1 AND 前真底70存在2; 

{1.2.2.7 结构70形成后存在24个K线，那么在24周期内，标注24周期的数字，并且判断24周期内有没有新低，如果创了新低，则在新低位置标注底结构消失}
底结构形成2:=(真底70信号2 AND REF(底钝化次数2, 1) >= 1) OR (底100形成2 AND 前真底70不存在2);
底有效范围2:=BARSLAST(底结构形成2)>0 AND BARSLAST(底结构形成2)<=24;
底范围标记2:=BARSLAST(底结构形成2);
底收盘价格2:=REF(BCL1, BARSLAST(底结构形成2));

底结构消失信号2:=底有效范围2 AND CLOSE < 底收盘价格2; 
底结构消失次数2:=COUNT(底结构消失信号2, 底范围标记2);
底结消存在2:=IF(ISVALID(BARSNEXT(CLOSE < 底收盘价格2)), BARSNEXT(CLOSE < 底收盘价格2) <= 24 - 底范围标记2, 0);
底结消不存在2:=底结消存在2==0 AND 底结构消失次数2==0;
底结构消失形成2:=底结消存在2 AND 底结构消失次数2==1 AND REF(底结构消失次数2, 1)==0;

{1.2.2.8 结构70形成后存在24个K线，那么在24周期内，底结构形成当前数字是0, 周期最后一个数字标注是24, 在这个周期范围内，第一次DIF >= 0，标注底平衡 }
底平衡次数2:=COUNT(DIF >= 0, 底范围标记2);
底平衡形成2:=底平衡次数2==1 AND DIF >= 0;

{1.2.2.9 标记所有与倒数第三个底部区域比较的标记点}
DRAWTEXT(底钝化有效2 AND 底钝化次数2=1, DIF * 0.7, IF(底钝化信号E2, '重底钝化', '底钝化')), COLORGREEN;
DRAWTEXT(真底70信号2 AND REF(底钝化次数2, 1) >= 1 AND DIF < DEA, DIF * 1.1, '底结构形成-70%'), COLORYELLOW;
DRAWTEXT(底100形成2, DIF * 0.3, '底结构形成-100%'), COLORMAGENTA;

DRAWTEXT(底结构消失形成2 , DIF * 0.4, '底结构消失');
DRAWNUMBER(底结消不存在2 AND 底有效范围2, HIGH * 0.5, 底范围标记2);
DRAWTEXT(底结消不存在2 AND 底有效范围2 AND 底平衡形成2, 0, '底平衡'), COLORRED;

{1.3 顶背离逻辑处理} 

{1.3.1 最近的顶部区域与倒数第二个顶部区域比较}
区域最高DIF:=IF(剩余区域长度 < 0, TIH1, REFX(HHV(DIF, T1), 剩余区域长度));
区域最高DEA:=IF(剩余区域长度 < 0, TEH1, REFX(HHV(DEA, T1), 剩余区域长度));

钝化区域TIH:=HHV(DIF, T1); {最近一次金叉区间的钝化区间的最高DIF, 钝化区间是第二根K线开始的区间}
钝化最高DIF:=IF(剩余区域长度 < 0, 钝化区域TIH, REFX(HHV(DIF, T1 - 1), 剩余区域长度));

{1.3.1.1 找到顶钝化点(寻找比前一个顶部区域最高收盘价更高的收盘价, 
                      从这个收盘价开始寻找一个DIF比前区域最高DIF更低的DIF)}
顶钝化信号I1:=T1 >= 1 AND DIF > DEA AND TCH1 > TCH2 AND DIF < TIH2 AND TIH1 <= TIH2;
顶钝化信号E1:=T1 >= 1 AND DIF > DEA AND TCH1 > TCH2 AND DEA < TEH2 AND TEH1 <= TEH2;

{1.3.1.2 找到顶钝化消失点(找到比上一个顶部区域更高的DIF)}
顶钝消信号1:=T1 >= 2 AND DIF > DEA AND DIF > TIH2 AND REF(COUNT(顶钝化信号I1, T1), 1) >= 1;

{1.3.1.3 找到顶结构形成-70% (从顶钝化往后开始，只要找到一个DIF比前一根DIF更小的情况)}
真顶70信号1:=T1 >= 2 AND REF(DIF, 1) > REF(DEA, 1) AND REF(DIF, 1) < TIH2 AND REF(DIF, 1) = 钝化最高DIF;
前真顶70存在1:=ISVALID(BARSLAST(真顶70信号1)) AND BARSLAST(真顶70信号1) < T1;
前真顶70不存在1:=前真顶70存在1==0;
后真顶70存在1:=ISVALID(REFX(BARSNEXT(真顶70信号1), 1));
后真顶70不存在1:=后真顶70存在1=0;

虚顶70信号1:=T1 >= 2 AND DIF > DEA AND DIF < REF(DIF, 1) AND DIF < TIH2 AND REF(DIF, 1) < 钝化最高DIF;
后虚顶70存在1:=ISVALID(REFX(BARSNEXT(虚顶70信号1), 1)) AND (后真顶70不存在1 OR REFX(BARSNEXT(真顶70信号1), 1) > REFX(BARSNEXT(虚顶70信号1), 1));
后虚顶70不存在1:=后虚顶70存在1=0;

{1.3.1.4 判断顶顿消情况是否存在}
前顶钝消次数1:=REF(COUNT(顶钝消信号1, T1), 1);
总顶钝消次数1:=REFX(COUNT(顶钝消信号1, T1), 剩余区域长度);
顶钝消不存在1:=前顶钝消次数1=0 AND 总顶钝消次数1=0;

{1.3.1.5 找到第一个有效的顶钝化点(钝化点之后如何存在结构反转的情况, 则钝化无效)
	     结构反转: 在出现顶结构-70%之后，出现更高的DIF, 则视为结构反转}
顶钝化有效1:=顶钝化信号I1 AND 顶钝消不存在1 AND (后真顶70不存在1 OR (后真顶70存在1 AND 后虚顶70不存在1) OR BARSNEXT(虚顶70信号1) < BARSNEXT(真顶70信号1));
顶钝化次数1:=COUNT(顶钝化有效1, T1);

{1.3.1.7 找到顶结构形成-100% (如果未出现顶钝化消失，则在CROSS(DEA, DIF)时, 标注顶结构形成-100%)};
顶100形成1:=DIF < DEA AND REF(DIF, 1) > REF(DEA, 1) AND REF(顶钝化次数1, 1) >= 1 AND 前真顶70存在1;

{1.3.1.6 判断是否需要比较倒数第三个区域}
总顶钝化次数:=REFX(COUNT(顶钝化有效1, T1), 剩余区域长度);
总收盘价大于:=REFX(TCH1, 剩余区域长度) > TCH2;
需顶钝化检测:=总顶钝化次数 = 0 AND 总收盘价大于;

{1.3.1.8 结构70形成后存在24个K线，那么在24周期内，标注24周期的数字，并且判断24周期内有没有新高，如果创了新高，则在新高位置标注顶结构消失}
顶结构形成1:=(真顶70信号1 AND REF(顶钝化次数1, 1) >= 1) OR (顶100形成1 AND 前真顶70不存在1);
顶有效范围1:=BARSLAST(顶结构形成1)>0 AND BARSLAST(顶结构形成1)<=24;
顶范围标记1:=BARSLAST(顶结构形成1);
顶收盘价格1:=REF(TCH1, BARSLAST(顶结构形成1));

顶结构消失信号1:=顶有效范围1 AND CLOSE > 顶收盘价格1; 
顶结构消失次数1:=COUNT(顶结构消失信号1, 顶范围标记1);

顶结消存在1:=IF(ISVALID(BARSNEXT(CLOSE > 顶收盘价格1)), BARSNEXT(CLOSE > 顶收盘价格1) <= 24 - 顶范围标记1, 0);
顶结消不存在1:=顶结消存在1==0 AND 顶结构消失次数1==0;
顶结构消失形成1:=顶结消存在1 AND 顶结构消失次数1==1 AND REF(顶结构消失次数1, 1)==0;

{1.3.1.9 结构70形成后存在24个K线，那么在24周期内，顶结构形成当前数字是0, 周期最后一个数字标注是24, 在这个周期范围内，第一次DIF <= 0，标注顶平衡 }
顶平衡次数1:=COUNT(DIF <= 0, 顶范围标记1);
顶平衡形成1:=顶平衡次数1==1 AND DIF <= 0;

{1.3.1.10 标记所有与倒数第二个顶部区域比较的标记点}
DRAWTEXT(顶钝化有效1 AND 顶钝化次数1=1, DIF * 0.7, IF(顶钝化信号E1, '重顶钝化', '顶钝化')), COLORGREEN;
DRAWTEXT(真顶70信号1 AND REF(顶钝化次数1, 1) >= 1 AND DIF > DEA, DIF * 1.1, '顶结构形成-70%'), COLORYELLOW;
DRAWTEXT(顶100形成1, DIF * 0.3, '顶结构形成-100%'), COLORMAGENTA;

DRAWTEXT(顶结构消失形成1, DIF * 0.4, '顶结构消失');
DRAWNUMBER(顶结消不存在1 AND 顶有效范围1, HIGH * 0.5, 顶范围标记1);
DRAWTEXT(顶结消不存在1 AND 顶有效范围1 AND 顶平衡形成1, 0, '顶平衡'), COLORRED;

{1.3.2 最近的顶部区域与倒数第三个顶部区域比较} 

{1.3.2.1 找到顶钝化点(寻找比倒数第三个顶部区域最高收盘价更高的收盘价, 
                      从这个收盘价开始寻找一个DIF比前区域最高DIF更低的DIF)}
顶钝化信号I2:=T1 >= 1 AND DIF > DEA AND TCH1 > TCH3 AND DIF < TIH3 AND TIH1 <= TIH3;
顶钝化信号E2:=T1 >= 1 AND DIF > DEA AND TCH1 > TCH3 AND DEA < TEH3 AND TEH1 <= TEH3;

{1.3.2.2 找到顶钝化消失点(找到比倒数第三个顶部区域更高的DIF)}
顶钝消信号2:=T1 >= 2 AND DIF > DEA AND DIF > TIH3 AND REF(COUNT(顶钝化信号I2, T1), 1) >= 1;

{1.3.2.3 找到顶结构形成-70% (从顶钝化往后开始，只要找到一个DIF比前一根DIF更小的情况)}
真顶70信号2:=T1 >= 2 AND REF(DIF, 1) > REF(DEA, 1) AND REF(DIF, 1) < TIH3 AND REF(DIF, 1) = 钝化最高DIF;
前真顶70存在2:=ISVALID(BARSLAST(真顶70信号2)) AND BARSLAST(真顶70信号2) < T1;
前真顶70不存在2:=前真顶70存在2==0;
后真顶70存在2:=ISVALID(REFX(BARSNEXT(真顶70信号2), 1));
后真顶70不存在2:=后真顶70存在2=0;

虚顶70信号2:=T1 >= 2 AND DIF > DEA AND DIF < REF(DIF, 1) AND DIF < TIH3 AND REF(DIF, 1) < 钝化最高DIF;
后虚顶70存在2:=ISVALID(REFX(BARSNEXT(虚顶70信号2), 1)) AND (后真顶70不存在2 OR REFX(BARSNEXT(真顶70信号2), 1) > REFX(BARSNEXT(虚顶70信号2), 1));
后虚顶70不存在2:=后虚顶70存在2=0;

{1.3.2.4 判断顶顿消情况是否存在}
前顶钝消次数2:=REF(COUNT(顶钝消信号2, T1), 1);
总顶钝消次数2:=REFX(COUNT(顶钝消信号2, T1), 剩余区域长度);
顶钝消不存在2:=前顶钝消次数2=0 AND 总顶钝消次数2=0;

{1.3.2.5 找到第一个有效的顶钝化点(钝化点之后如何存在结构反转的情况, 则钝化无效)
	     结构反转: 在出现顶结构-70%之后，出现更高的DIF, 则视为结构反转}
顶钝化有效2:=需顶钝化检测 AND 顶钝化信号I2 AND 顶钝消不存在2 AND (后真顶70不存在2 OR (后真顶70存在2 AND 后虚顶70不存在2) OR BARSNEXT(虚顶70信号2) < BARSNEXT(真顶70信号2));
顶钝化次数2:=COUNT(顶钝化有效2, T1);

{1.3.2.6 找到顶结构形成-100% (如果未出现顶钝化消失，则在CROSS(DEA, DIF)时, 标注顶结构形成-100%)};
顶100形成2:=DIF < DEA AND REF(DIF, 1) > REF(DEA, 1) AND REF(顶钝化次数2, 1) >= 1 AND 前真顶70存在2;

{1.3.2.7 结构70形成后存在24个K线，那么在24周期内，标注24周期的数字，并且判断24周期内有没有新高，如果创了新高，则在新高位置标注顶结构消失}
顶结构形成2:=(真顶70信号2 AND REF(顶钝化次数2, 1) >= 1) OR (顶100形成2 AND 前真顶70不存在2);
顶有效范围2:=BARSLAST(顶结构形成2)>0 AND BARSLAST(顶结构形成2)<=24;
顶范围标记2:=BARSLAST(顶结构形成2);
顶收盘价格2:=REF(TCH1, BARSLAST(顶结构形成2));

顶结构消失信号2:=顶有效范围2 AND CLOSE > 顶收盘价格2; 
顶结构消失次数2:=COUNT(顶结构消失信号2, 顶范围标记2);

顶结消存在2:=IF(ISVALID(BARSNEXT(CLOSE > 顶收盘价格2)), BARSNEXT(CLOSE > 顶收盘价格2) <= 24 - 顶范围标记2, 0);
顶结消不存在2:=顶结消存在2==0  AND 顶结构消失次数2==0;
顶结构消失形成2:=顶结消存在2 AND 顶结构消失次数2==1 AND REF(顶结构消失次数2, 1)==0;

{1.3.2.8 结构70形成后存在24个K线，那么在24周期内，顶结构形成当前数字是0, 周期最后一个数字标注是24, 在这个周期范围内，第一次DIF <= 0，标注顶平衡 }
顶平衡次数2:=COUNT(DIF <= 0, 顶范围标记2);
顶平衡形成2:=顶平衡次数2==1 AND DIF <= 0;

{1.3.2.9 标记所有与倒数第三个顶部区域比较的标记点}
DRAWTEXT(顶钝化有效2 AND 顶钝化次数2=1, DIF * 0.7, IF(顶钝化信号E2, '重顶钝化', '顶钝化')), COLORGREEN;
DRAWTEXT(真顶70信号2 AND REF(顶钝化次数2, 1) >= 1 AND DIF > DEA, DIF * 1.1, '顶结构形成-70%'), COLORYELLOW;
DRAWTEXT(顶100形成2, DIF * 0.3, '顶结构形成-100%'), COLORMAGENTA;

DRAWTEXT(顶结构消失形成2 , DIF * 0.4, '顶结构消失');
DRAWNUMBER(顶结消不存在2 AND 顶有效范围2, HIGH * 0.5, 顶范围标记2);
DRAWTEXT(顶结消不存在2 AND 顶有效范围2 AND 顶平衡形成2, 0, '顶平衡'), COLORRED;
